apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'
    flavorDimensions 'screenSize'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25

        applicationId 'org.cocos2dx.hellocpp'

        versionCode 1
        versionName '1.0.0'

        multiDexEnabled true

        externalNativeBuild {
            ndkBuild {
                targets 'cocos2dcpp'

                arguments 'NDK_TOOLCHAIN_VERSION=clang'
                arguments 'APP_STL=c++_static'
                arguments 'APP_PLATFORM=android-9'
                arguments 'APP_LDFLAGS=-latomic'

                // http://stackoverflow.com/questions/42579759/how-to-change-android-gradle-ndk-j-jobs-flag-value-for-clean-task
                arguments '-j' + Runtime.runtime.availableProcessors()

                cppFlags += '-frtti'
                cppFlags += '-fsigned-char'
            }
        }

        ndk {
            abiFilters 'armeabi', 'armeabi-v7a' //, 'x86'
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    sourceSets.main {
    }

    signingConfigs {
        release {
            // Use your key store information here.
            keyAlias 'key_alias'
            keyPassword 'key_password'
            storeFile file('file_path')
            storePassword 'store_password'
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            externalNativeBuild {
                ndkBuild {
                    arguments 'NDK_DEBUG=0'
                    cppFlags += '-DCOCOS2D_DEBUG=1'
                }
            }
        }
        debug {
            externalNativeBuild {
                ndkBuild {
                    arguments 'NDK_DEBUG=1'
                    cppFlags += '-DNDEBUG'
                }
            }
        }
    }

    productFlavors {
        iphone {
            dimension 'screenSize'
            manifestPlaceholders = [
                    xlargeScreens: "false",
                    largeScreens : "false",
                    normalScreens: "false",
                    smallScreens : "true",
            ]
        }
        iphonehd {
            dimension 'screenSize'
            manifestPlaceholders = [
                    xlargeScreens: "false",
                    largeScreens : "true",
                    normalScreens: "true",
                    smallScreens : "false",
            ]
        }
        ipadhd {
            dimension 'screenSize'
            manifestPlaceholders = [
                    xlargeScreens: "true",
                    largeScreens : "false",
                    normalScreens: "false",
                    smallScreens : "false",
            ]
        }
    }

    packagingOptions {
        // Exclude metadata files.
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':libcocos2dx')

    compile 'com.android.support:multidex:1.0.1'
}
